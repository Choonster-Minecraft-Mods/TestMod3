import groovy.transform.Field

import java.util.jar.Attributes
import java.util.jar.JarFile
import java.util.jar.Manifest
import java.util.zip.ZipInputStream

buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

plugins {
    id 'org.ajoberstar.grgit' version '1.7.2'
}

apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

final commitId

if (hasProperty("grgit")) { // If there's a valid Git repository, get the latest commit ID
    commitId = "${grgit.head().abbreviatedId}"
} else { // Else fall back to NOGIT
    commitId = "NOGIT"
}

version = "${mc_version}-${mod_version}-${commitId}"
group = "${package_group}" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${mod_name}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: project.mappings_channel, version: project.mappings_version

    // makeObfSourceJar = false // an SRG named sources jar is made by default. uncomment this to disable.

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                testmod3 {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                testmod3 {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'testmod3', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                testmod3 {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }


repositories {
    mavenCentral()
    maven { url = "http://dvs1.progwml6.com/files/maven/" }
    maven { url = "https://maven.tehnut.info" }
    maven { url = 'https://jitpack.io/' }

    maven { // TOP
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
}

configurations {
    embed {
        transitive = false
    }

    compile.extendsFrom embed
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    compileOnly fg.deobf("mezz.jei:jei-1.16.4:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.16.4:${jei_version}")

    embed "org.apache.httpcomponents:fluent-hc:${httpclient_version}"

    // Compile against the Hwyla API, but do not include it at runtime
    compileOnly fg.deobf("mcp.mobius.waila:Hwyla:${hwyla_version}:api")
    // At runtime, use the full Hwyla jar
    runtimeOnly fg.deobf("mcp.mobius.waila:Hwyla:${hwyla_version}")

    compileOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-1.16:${top_version}:api")
    runtimeOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-1.16:${top_version}")

//    deobfCompile "com.github.azanor:Baubles:${baubles_version}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.addAll(['-Xlint:unchecked', '-Xlint:deprecation'])
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    /*
     TODO: Uncomment when Hwyla fixes its Javadoc errors or I find a way to ignore them
     https://forums.minecraftforge.net/topic/92633-ignoring-javadoc-errors-in-dependencies/
     https://github.com/TehNut/HWYLA/issues/303
     https://stackoverflow.com/questions/64619620/ignoring-javadoc-errors-in-dependencies
     */
//    archives javadocJar
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Test Mod 3",
                "Specification-Vendor"    : "Choonster",
                "Specification-Version"   : "${mc_version}-${mod_version}",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Choonster",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        ])
    }
}

// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

// makeLibraryMetas task adapted from covers1624's ContainedDepsPlugin:
// https://github.com/covers1624/GradleStuff
// TODO: Contained dependencies are disabled in 1.14, re-evaluate when they're re-enabled
@Field def MAVEN_ARTIFACT = new Attributes.Name("Maven-Artifact")

task makeLibraryMetas {
    outputs.upToDateWhen { false }

    def temp = getTemporaryDir()
    if (temp.exists()) {
        delete temp.listFiles()
    }

    def configuration = configurations.embed

    configuration.getResolvedConfiguration().getResolvedArtifacts().forEach { dep ->
        def file = dep.getFile()
        def mavenArtifact = dep.getId().getComponentIdentifier().toString()
        if (!hasMavenArtifactAttribute(file)) {
            def manifest = new Manifest()
            def attributes = manifest.getMainAttributes()
            attributes.put Attributes.Name.MANIFEST_VERSION, "1.0"
            attributes.put MAVEN_ARTIFACT, mavenArtifact

            new File(temp, file.getName() + ".meta").withOutputStream { os ->
                manifest.write(os)
            }
        }
    }

    def str = configuration.collect { file -> file.getName() }.join(" ")
    def tasks = project.tasks.withType(Jar.class)
    tasks.each { task -> task.from temp, { CopySpec spec -> spec.rename '(.+.jar.meta)', 'META-INF/libraries/$1' } }
    tasks.each { task -> task.from configuration, { CopySpec spec -> spec.rename '(.+.jar)', 'META-INF/libraries/$1' } }
    tasks.each { task -> task.getManifest().getAttributes().putAt 'ContainedDeps', str }
}

tasks.withType(Jar.class).each { task ->
    task.dependsOn(makeLibraryMetas)
}

boolean hasMavenArtifactAttribute(File file) {
    def zi = new ZipInputStream((InputStream) new FileInputStream(file))

    def ze = zi.getNextEntry()
    while (ze != null) {
        if (ze.getName().equalsIgnoreCase(JarFile.MANIFEST_NAME)) {
            def manifest = new Manifest((InputStream) zi)
            if (manifest.getMainAttributes().containsKey(MAVEN_ARTIFACT)) {
                zi.closeQuietly()
                return true
            }
        }
        ze = zi.getNextEntry()
    }

    try {
        zi.close()
    }
    catch (ignored) {
    }

    return false
}